#!/usr/bin/env perl

use strict;
use warnings;
use utf8;

use File::Basename;
use File::Spec;

use lib File::Spec->catdir(File::Basename::dirname(__FILE__), 'lib');

use StackTrace::Pretty::Printer;

my $printer = StackTrace::Pretty::Printer->new();

my $CARP_OUTPUT_PATTERN = qr/at (\S+) line (\d+)\.?$/;

while (my $line = <STDIN>) {
    chomp($line);

    unless ($line =~ $CARP_OUTPUT_PATTERN) {
        print "$line\n";
        next;
    } 

    process_carp_line(0, $line);

    print "\n";
    my $depth = 1;
    while (my $_line = <STDIN>) {
        chomp($_line);

        unless ($_line =~ /^\s/) {
            print "$_line\n";
            last;
        }

        process_carp_line($depth, $_line);
        print "\n";
        $depth++;
    }
}

sub process_carp_line {
    my ($depth, $line) = @_;

    my ($filename, $target_line_num) = $line =~ $CARP_OUTPUT_PATTERN;
    $line =~ s/^\s+//;
    print "[$depth]\n";

    $printer->print({
        filename => $filename,
        lineno => $target_line_num,
        raw => $line,
    });
}
